programa PFinal
procesos
  proceso JuntarEsquina (ES flores: numero; ES papeles: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores:= flores + 1
    mientras HayPapelEnLaEsquina
      tomarPapel
      papeles:= papeles + 1
  fin

areas
  areaRecorrido: AreaPC (1,1,9,9)
  areaEspera: AreaPC (20,1,20,5)
  areaJefe: AreaP (20,15,20,15)

robots
  robot tipo1
  variables
    calle, cruce: numero
    flores, papeles: numero
    subiendo: boolean
    vertical: boolean
  comenzar
    subiendo:= V
    cruce:= 2
    flores:= 0
    papeles:= 0
    RecibirMensaje (vertical, jefe)
    RecibirMensaje (calle, jefe)
    
    si vertical
      Pos(calle,1)
    sino
      Pos (1, calle)
      derecha
    Informar (PosCa)
    repetir 2
      repetir 4
        JuntarEsquina (flores, papeles)
        si vertical
          BloquearEsquina (PosAv, cruce)
        sino
          BloquearEsquina (cruce, PosCa)
        mover
        JuntarEsquina (flores, papeles)
        mover
        si vertical
          LiberarEsquina(PosAv, cruce)
        sino
          LiberarEsquina (cruce, PosCa)
        si subiendo
          cruce:= cruce + 2
        sino
          cruce:= cruce - 2
        
      derecha
      repetir 2
        JuntarEsquina (flores, papeles)
        mover
      derecha
      cruce:= 8
      subiendo:= F
    
    EnviarMensaje (flores, jefe)
    EnviarMensaje (papeles, jefe)
    
  fin
  
  
  robot tipoJefe
  variables
    flores, papeles, floresT, papelesT: numero
  comenzar
    flores:= 0
    papeles:= 0
    floresT:= 0
    papelesT:= 0
    
    
    EnviarMensaje (V, robot1)
    EnviarMensaje (V, robot2)
    EnviarMensaje (F, robot3)
    EnviarMensaje (F, robot4)

    
    EnviarMensaje (2, robot1)
    EnviarMensaje (6, robot2)
    EnviarMensaje (4, robot3)
    EnviarMensaje (8, robot4)
    
    
    
    repetir 4
      RecibirMensaje (flores, *)
      RecibirMensaje (papeles, *)
      floresT:= floresT + flores
      papelesT:= papelesT + papeles
    
    Informar (floresT)
    Informar (papelesT)  
  fin

variables
  robot1: tipo1
  robot2: tipo1
  robot3: tipo1
  robot4: tipo1
  
  jefe: tipoJefe

comenzar
  AsignarArea (robot1, areaRecorrido)
  AsignarArea (robot1, areaEspera)
  AsignarArea (robot2, areaRecorrido)
  AsignarArea (robot2, areaEspera)
  AsignarArea (robot3, areaRecorrido)
  AsignarArea (robot3, areaEspera)
  AsignarArea (robot4, areaRecorrido)
  AsignarArea (robot4, areaEspera)

  AsignarArea (jefe, areaJefe)
  
  Iniciar (robot1, 20,1)
  Iniciar (robot2, 20,2)
  Iniciar (robot3, 20,4)
  Iniciar (robot4, 20,5)
  Iniciar (jefe, 20, 15)
  
fin





